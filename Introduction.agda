{-# OPTIONS --without-K #-}

module Introduction where

open import Agda.Primitive

{- excerpt from HoTT book, p. 11: Comparing points of view on type-theoretic operations
| Logic               | Types           |
| :------------------ | :-------------- |
| proposition         | A               |
| proof               | a : A           |
| âŠ¥, âŠ¤                |  ğŸ˜, ğŸ™           |
| A âˆ¨ B               | A + B           |
| A âˆ§ B               | A Ã— B           |
| A âŸ¹ B              |  A â†’ B          |
| âˆƒ x âˆˆ A . B(x)      | Î£ (x:A) B(x)    |
| âˆ€ x âˆˆ A . B(x)      | Î  (x:A) B(x)    |
| equality =          | Id_A            |
| predicate           | B(x)            |
| conditional proof   | b(x) : B(x)     |
-}


{-
## Symbols
In Emacs' agda-mode we can easily write many Unicode symbols, e.g. ğ”¹, â„•, â†’, Î±, â€¦
List of symbols for example here: https://people.inf.elte.hu/divip/AgdaTutorial/Symbols.html
Some frequently used symbols:
* \Ga â†’ Î±, \Gb Î², ...
* \b{B,N,0,1,...} â†’ ğ”¹, â„•, ğŸ˜, ğŸ™, ...
* \st â†’ â‹† âœ¦ âœ§ âœ¶ ... (different stars, check status bar and move the cursor to select one)
* \lr (pos. 7) â†’ âŸº
* \== â†’ â‰¡
* \< â†’ âŸ¨
* \> â†’ âŸ©
* \qed â†’ âˆ

## Emacs cheat sheet (C - Ctrl; M - meta key, usually Alt or Windows key)
C-c C-l save + load file into agda
C-c C-c case split
C-c C-s solve constraints

M-x describe-char (move cursor over a symbol, then enter the command, it will show you how to type the symbol)

C-x C-s save file
C-x C-c exit
-}


-- Types 'live' in so called universes (sorts).
-- The fundamental universe is called 'Set'.


------------------------------------------------------------------------
-- Define basic data types
------------------------------------------------------------------------
data ğŸ˜ : Set where

data ğŸ™ : Set where
  â˜… : ğŸ™

data ğ”¹ : Set where
  false : ğ”¹
  true : ğ”¹

-- example: write a function using ğ”¹
_&&_ : ğ”¹ â†’ ğ”¹ â†’ ğ”¹
false && y = false
true  && y = y

-- example: mixfix notation
if_then_else_ : {A : Set} â†’ ğ”¹ â†’ A â†’ A â†’ A
if false then x else y = y
if true  then x else y = x

------------------------------------------------------------------------
-- dependent pair type
------------------------------------------------------------------------
record Î£ {â„“â‚ â„“â‚‚} (A : Set â„“â‚) (B : A â†’ Set â„“â‚‚) : Set (â„“â‚ âŠ” â„“â‚‚) where
  constructor
    _,_
  field
    Ï€â‚ : A
    Ï€â‚‚ : B Ï€â‚


-- product type
_Ã—_ : âˆ€ {â„“â‚ â„“â‚‚} â†’ âˆ€ (A : Set â„“â‚) (B : Set â„“â‚‚) â†’ Set (â„“â‚ âŠ” â„“â‚‚)
A Ã— B = Î£ A (Î» x â†’ B)

-- examples
_ : ğ”¹ Ã— ğ”¹
_ = (true , false)

_ : Î£ ğ”¹ (Î» {true â†’ ğ”¹ ; false â†’ ğŸ™})
_ = false , â˜…
--_ = true , true
--_ = true , false

------------------------------------------------------------------------
-- coproduct type
------------------------------------------------------------------------
data _âŠ_ {â„“â‚ â„“â‚‚} (A : Set â„“â‚) (B : Set â„“â‚‚) : Set (â„“â‚ âŠ” â„“â‚‚) where
  inl : (a : A) â†’ A âŠ B
  inr : (b : B) â†’ A âŠ B

-- example
_ : ğ”¹ âŠ ğŸ™
_ = inl true
--_ = inr â˜…

------------------------------------------------------------------------
-- Identity type / Propositional equality
------------------------------------------------------------------------
data _â‰¡_ {â„“} {A : Set â„“} : A â†’ A â†’ Set â„“ where
  refl : âˆ€ {x} â†’ x â‰¡ x

-- symmetry of â‰¡
symâ‰¡ : âˆ€ {â„“} {A : Set â„“} {a b : A} â†’ a â‰¡ b â†’ b â‰¡ a
symâ‰¡ refl = refl

-- transitivity of â‰¡
_âˆ˜_ : âˆ€ {â„“} {A : Set â„“} {a b c : A} â†’
      (a â‰¡ b) â†’ (b â‰¡ c) â†’ (a â‰¡ c)
refl âˆ˜ refl = refl

-- function composition
_âˆ™_ : âˆ€{â„“â‚ â„“â‚‚ â„“â‚ƒ} {A : Set â„“â‚} {B : Set â„“â‚‚} {C : Set â„“â‚ƒ}
      (f : B â†’ C) â†’ (g : A â†’ B) â†’ (A â†’ C)
f âˆ™ g = Î» a â†’ f (g a)

------------------------------------------------------------------------
-- Equivalence
record Eq {â„“â‚ â„“â‚‚} (A : Set â„“â‚) (B : Set â„“â‚‚) : Set (â„“â‚ âŠ” â„“â‚‚) where
  --constructor makeEq
  constructor Â»âŸ¹Â«:_Â»âŸ¸Â«:_âˆ
  field
    fun : A â†’ B
    inv : B â†’ A

syntax Eq A B = A âŸº B

symEq : âˆ€{â„“â‚ â„“â‚‚} {A : Set â„“â‚} {B : Set â„“â‚‚} â†’ Eq A B â†’ Eq B A
symEq i = Â»âŸ¹Â«: (Eq.inv i)
          Â»âŸ¸Â«: (Eq.fun i) âˆ

transEq : âˆ€{â„“â‚ â„“â‚‚ â„“â‚ƒ} {A : Set â„“â‚} {B : Set â„“â‚‚} {C : Set â„“â‚ƒ} â†’
          Eq A B â†’ Eq B C â†’ Eq A C
transEq i j = Â»âŸ¹Â«: Eq.fun j âˆ™ Eq.fun i
              Â»âŸ¸Â«: Eq.inv i âˆ™ Eq.inv j âˆ


------------------------------------------------------------------------
-- Problem:
-- Identity and equivalence proofs get confusing fast, because
-- when using _âˆ˜_ and transEq we don't see any intermediate steps.
exâˆ˜ : {A : Set} {a b c : A} â†’ (p : a â‰¡ b) â†’ (q : c â‰¡ b) â†’ a â‰¡ c
exâˆ˜ {A} {a} {b} {c} p q = p âˆ˜ (symâ‰¡ q)

-- Solution:
-- â‰¡-Reasoning and âŸº-Reasoning modules, which define nice syntax for displaying intermediate steps


module â‰¡-Reasoning {a} {A : Set a} where
  -- aus stdlib Ã¼bernommen

  infix  3 _âˆ
  infixr 2 _â‰¡âŸ¨âŸ©_ step-â‰¡ step-â‰¡Ë˜
  infix  1 begin_

  begin_ : âˆ€{x y : A} â†’ x â‰¡ y â†’ x â‰¡ y
  begin_ xâ‰¡y = xâ‰¡y

  _â‰¡âŸ¨âŸ©_ : âˆ€ (x {y} : A) â†’ x â‰¡ y â†’ x â‰¡ y
  _ â‰¡âŸ¨âŸ© xâ‰¡y = xâ‰¡y

  step-â‰¡ : âˆ€ (x {y z} : A) â†’ y â‰¡ z â†’ x â‰¡ y â†’ x â‰¡ z
  step-â‰¡ _ yâ‰¡z xâ‰¡y = xâ‰¡y âˆ˜ yâ‰¡z

  step-â‰¡Ë˜ : âˆ€ (x {y z} : A) â†’ y â‰¡ z â†’ y â‰¡ x â†’ x â‰¡ z
  step-â‰¡Ë˜ _ yâ‰¡z yâ‰¡x = (symâ‰¡ yâ‰¡x) âˆ˜ yâ‰¡z

  _âˆ : âˆ€ (x : A) â†’ x â‰¡ x
  _âˆ _ = refl

  syntax step-â‰¡  x yâ‰¡z xâ‰¡y = x â‰¡âŸ¨  xâ‰¡y âŸ© yâ‰¡z
  syntax step-â‰¡Ë˜ x yâ‰¡z yâ‰¡x = x â‰¡Ë˜âŸ¨ yâ‰¡x âŸ© yâ‰¡z


open â‰¡-Reasoning

-- example:
exReasoning : {A : Set} {a b c : A} â†’ (p : a â‰¡ b) â†’ (q : c â‰¡ b) â†’ a â‰¡ c
exReasoning {A} {a} {b} {c} p q = begin
    a
  â‰¡âŸ¨ p âŸ©
    b
  â‰¡Ë˜âŸ¨ q âŸ©
    c âˆ

------------------------------------------------------------------------
--- Natural numbers and induction
------------------------------------------------------------------------
data â„• : Set where
  zero : â„•
  succ : â„• â†’ â„•

one = succ zero
two = succ (succ zero)

_+_ : â„• â†’ â„• â†’ â„•
zero + n = n
succ m + n = succ (m + n)

_*_ : â„• â†’ â„• â†’ â„•
zero * n = zero
succ m * n = n + (m * n)

-- congruence (functions respect equalities; sometimes called ap)
cong : {A B : Set} {a b : A} â†’ (f : A â†’ B) â†’ a â‰¡ b â†’ f a â‰¡ f b
cong f refl = refl



-- To perform induction proofs we usually need a recursive call
-- and cong to exhibit the induction step

+-zero : (m : â„•) â†’ (m + zero) â‰¡ m
+-zero zero = refl
+-zero (succ m) = cong succ (+-zero m)

+-succ : (m n : â„•) â†’ (m + succ n) â‰¡ (succ m + n)
+-succ zero n = refl
+-succ (succ m) n = cong succ (+-succ m n)

+-comm : (m n : â„•) â†’ (m + n) â‰¡ (n + m)
+-comm zero n = symâ‰¡ (+-zero n)
+-comm (succ m) n = begin
    (succ m + n)
  â‰¡âŸ¨âŸ©
    succ (m + n)
  â‰¡âŸ¨ cong succ (+-comm m n) âŸ©
    succ (n + m)
  â‰¡âŸ¨âŸ©
    succ n + m
  â‰¡Ë˜âŸ¨ +-succ n m âŸ©
    (n + succ m) âˆ

n+nâ‰¡n*2 : (n : â„•) â†’ (n + n) â‰¡ (n * two)
n+nâ‰¡n*2 zero = refl
n+nâ‰¡n*2 (succ n) = begin
    succ (n + succ n)
  â‰¡âŸ¨ cong succ (+-succ n n) âŸ©
    succ (succ n + n)
  â‰¡âŸ¨âŸ©
    succ (succ (n + n))
  â‰¡âŸ¨âŸ©
    two + (n + n)
  â‰¡âŸ¨ cong (two +_) (n+nâ‰¡n*2  n) âŸ©
    two + (n * two)
  â‰¡âŸ¨âŸ©
    (succ n * two) âˆ



------------------------------------------------------------------------
-- Universe levels
------------------------------------------------------------------------

-- Problem: We want to put a function into out product, but Agda complains:
--_ : (ğ”¹ â†’ Set) Ã— ğŸ™
--_ = {!!}

-- What's the type of (ğ”¹ â†’ Set)? Agda can infer the type for you, type: C-c C-d ğ”¹ â†’ Set
-- Agda will tell you that it has type Setâ‚.

-- Solution: introduce universe levels:
-- Set is Setâ‚€
-- Setâ‚€ : Setâ‚
-- Setâ‚ : Setâ‚‚
-- ...

-- same problem with another example:
--_ : {p q : Set} â†’ (q â‰¡ p) Ã—' ğŸ™
--_ = ?
